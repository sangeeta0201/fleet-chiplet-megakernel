/* Copyright 2025 CMU
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
#pragma once
#include "bfloat16.h"
#include "worker_config.h"
#include <cmath>

namespace kernel {
using bfloat16 = type::bfloat16_t;

constexpr float log2e = 1.44269504088896340736f;

constexpr int log2_constexpr(int n, int p = 0) {
  return (n <= 1) ? p : log2_constexpr(n >> 1, p + 1);
}

constexpr int max_power_of_two_le(int x) {
  if (x <= 0) {
    return 0;
  }
  int result = 1;
  while ((result << 1) <= x) {
    result <<= 1;
  }
  return result;
}

__device__ __forceinline__ void
    convert_32_f32_to_16_bf16_uint32(float const (&s_frag)[32],
                                     uint32_t (&a_frag)[16]) {
#pragma unroll
  for (int i = 0; i < 16; ++i) {
    bfloat16 low = bfloat16(s_frag[2 * i]);
    bfloat16 high = bfloat16(s_frag[2 * i + 1]);
    a_frag[i] = (static_cast<uint32_t>(high.storage) << 16) | low.storage;
  }
}

__device__ __forceinline__ void
    convert_f32_to_bf16_uint32(float const (&s_frag)[8],
                               uint32_t (&a_frag)[4]) {
#pragma unroll
  for (int i = 0; i < 4; ++i) {
    bfloat16 low = bfloat16(s_frag[2 * i]);
    bfloat16 high = bfloat16(s_frag[2 * i + 1]);
    a_frag[i] = (static_cast<uint32_t>(high.storage) << 16) | low.storage;
  }
}

__forceinline__ __device__ float shfl_xor_sync(float x, int lane_mask) {
#if defined(__HIP_PLATFORM_AMD__) || defined(MIRAGE_AMD_MI300)
  // HIP: use __shfl_xor with width=64 for native AMD wavefronts
  return __shfl_xor(x, lane_mask, 64);
#else
  float y;
  asm volatile("shfl.sync.bfly.b32 %0, %1, %2, 0x1f, 0xffffffff;"
               : "=f"(y)
               : "f"(x), "r"(lane_mask));
  return y;
#endif
}

/*!
 * \brief Wrapper of PTX ex2.approx instruction, which computes 2^x
 * \param x input
 */
__device__ __forceinline__ float ptx_exp2(float x) {
#if !defined(__NVCC__)
  /* HIP/Clang: no PTX; use standard libm */
  return exp2f(x);
#else
  float y;
  asm volatile("ex2.approx.ftz.f32 %0, %1;" : "=f"(y) : "f"(x));
  return y;
#endif
}

/*!
 * \brief Wrapper of PTX lg2.approx instruction, which computes log2(x)
 * \param x input
 */
__forceinline__ __device__ float ptx_log2(float x) {
#if !defined(__NVCC__)
  /* HIP/Clang: no PTX; use standard libm */
  return log2f(x);
#else
  float y;
  asm volatile("lg2.approx.ftz.f32 %0, %1;" : "=f"(y) : "f"(x));
  return y;
#endif
}

static __device__ __forceinline__ int lane_id() {
  // Keyed off the compiler's own __gfx1250__, NOT mirage's MIRAGE_ARCH_GFX1250:
  // that macro is defined in arch_traits.cuh, which this header does not
  // include, so a guard spelled that way would compile cleanly and never fire
  // -- leaving the wave64 mask in place while looking fixed.
#if defined(__gfx1250__)
  // gfx1250 is wave32. The 0x3f mask below is not merely imprecise here, it is
  // wrong in a way that produces a plausible answer: `lane_id() == 0` is the
  // predicate that elects one thread per wave to write cross-wave reduction
  // scratch, and at wave32 it elects only threads 0, 64, 128, 192 -- one per
  // TWO waves. The odd waves' partials are never written, and the slots are
  // read back as whatever was in LDS.
  //
  // Measured, not inferred: with a 256-thread argmax over 1024 logits and the
  // true maximum placed at an index owned by thread 33 (wave 1), the block
  // reduction returned 16 instead of 1000. argmax is the last task in the
  // model -- it picks the emitted token -- so this is a wrong-output bug, not
  // a numerical one.
  return threadIdx.x & 0x1f;
#elif defined(__HIP_PLATFORM_AMD__) || defined(MIRAGE_AMD_MI300)
  // AMD gfx9: native 64-thread wavefront
  return threadIdx.x & 0x3f; // 64-thread wavefront
#else
  // CUDA: 32-thread warp
  return threadIdx.x & 0x1f; // 32-thread warp
#endif
}

static __device__ __forceinline__ int warp_id() {
  // Returns logical warp ID based on NUM_THREADS_PER_WARP
  // Simple division avoids shuffle-related hang issues on AMD
  return threadIdx.x / NUM_THREADS_PER_WARP;
}

template <typename T, int NUM_ELEMENTS>
__device__ __forceinline__ void clear_smem_buffer(T *buffer) {
  constexpr int total_bytes = NUM_ELEMENTS * sizeof(T);
  constexpr int num_128bit_writes = total_bytes / 16;
  constexpr int remaining_elements_offset =
      num_128bit_writes * (16 / sizeof(T));

  // Clear the bulk of the buffer using 128-bit writes
  for (int i = threadIdx.x; i < num_128bit_writes; i += NUM_THREADS) {
    ((__uint128_t *)buffer)[i] = 0ul;
  }

  // Handle the tail if the total size is not a multiple of 16 bytes
  if constexpr ((total_bytes % 16) != 0) {
    for (int i = remaining_elements_offset + threadIdx.x; i < NUM_ELEMENTS;
         i += NUM_THREADS) {
      buffer[i] = T(0.0f);
    }
  }
}

static __device__ __forceinline__ void clear_8_floats(float *buffer) {
  *((__uint128_t *)(buffer)) = 0ul;
  *((__uint128_t *)(buffer + 4)) = 0ul;
}

// Vectorized zero initialization struct
template <typename T, int N>
struct vec_zero_t {
  static __device__ __forceinline__ void fill_zero(T *ptr) {
    // Ensure sizeof(T) * N is a multiple of 16 bytes
    static_assert((sizeof(T) * N) % 16 == 0,
                  "sizeof(T) * N must be a multiple of 16 bytes for proper "
                  "vectorized operations");

    constexpr int total_bytes = sizeof(T) * N;
    constexpr int num_chunks = total_bytes / sizeof(__uint128_t);
    __uint128_t *vec_ptr = reinterpret_cast<__uint128_t *>(ptr);
    constexpr int max_iters = (num_chunks + NUM_THREADS - 1) / NUM_THREADS;

#pragma unroll
    for (int i = 0; i < max_iters; ++i) {
      int idx = i * blockDim.x + threadIdx.x;
      if (idx < num_chunks) {
        vec_ptr[idx] = 0ul;
      }
    }
  }
};

} // namespace kernel
